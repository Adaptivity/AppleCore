buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

minecraft {
    version = "1.7.10-10.13.0.1180"
}

group = project.projectDir.name.toLowerCase()
archivesBaseName = project.projectDir.name + "-mc" + project.minecraft.version

sourceSets.main{
    java {
		srcDirs 'java', 'apis' //set the source folder as the /src subfolder
	}
    resources {
		srcDirs 'resources' //set the resources folder as the /resources subfolder
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: '*.jar,*.zip')
}

// variable replacement in source files
task processSourcesCustom(type:Copy){
	from('java')
	{
		include '**/ModInfo.java'

		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	into project.buildDir.getPath()+"/sources/java"
}

task removeExampleAndReferenceFiles(type:Delete){
    delete += project.buildDir.getPath()+"/sources/java"+"/squeek/applecore/example"
    delete += project.buildDir.getPath()+"/sources/java"+"/squeek/applecore/reference"
    delete += file(sourceSets.main.output.resourcesDir.getPath()+"/mcmod.info")
}

// correct task ordering
compileJava.dependsOn processSourcesCustom
compileJava.dependsOn removeExampleAndReferenceFiles
afterEvaluate { project ->
	processSourcesCustom.shouldRunAfter(sourceMainJava)
    removeExampleAndReferenceFiles.shouldRunAfter(processSourcesCustom)
}

// Add Coremod Manifest
jar {
    manifest {
        attributes 'FMLCorePlugin': 'squeek.applecore.AppleCore'
        attributes 'FMLCorePluginContainsFMLMod': 'false'
    }
}

processResources
{
    // replace stuff in *.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '*.info'
    }
}